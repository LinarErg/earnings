
&НаСервере
Процедура СформироватьНаСервере()

	Обработка = РеквизитФормыВЗначение("Объект");	
	
	Макет = Обработка.ПолучитьМакет("Макет");
	
	ТабличныйДокумент = ТабДок;
	ТабличныйДокумент.Очистить();
	
	облЗаголовок 	= Макет.ПолучитьОбласть("ОблЗаголовок");
	облШапка 		= Макет.ПолучитьОбласть("облШапка");
	облСтрока 		= Макет.ПолучитьОбласть("ОблСтрока");
	облИтого 		= Макет.ПолучитьОбласть("облИтого");
	облГруппировкаЗаказчик = Макет.ПолучитьОбласть("облГруппировкаЗаказчик");

	ТабличныйДокумент.Вывести(облЗаголовок);
	ТабличныйДокумент.Вывести(облШапка);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказчик",?(ЗначениеЗаполнено(Заказчик),Заказчик,Неопределено));
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкиОстатки.Заказчик КАК Заказчик,
		|	ЗаявкиОстатки.Заявка КАК Заявка,
		|	ЗаявкиОстатки.Заявка.ВыполненнаяРабота КАК ЗаявкаВыполненнаяРабота,
		|	ЗаявкиОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.Заявки.Остатки КАК ЗаявкиОстатки
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &Заказчик = НЕОПРЕДЕЛЕНО
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЗаявкиОстатки.Заказчик = &Заказчик
		|		КОНЕЦ
		|ИТОГИ
		|	СУММА(СуммаОстаток)
		|ПО
		|	ОБЩИЕ,
		|	Заказчик";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаОбщийИтог.Следующий();		// Общий итог
	
	ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
	
	облИтого.Параметры.Сумма = ВыборкаОбщийИтог.СуммаОстаток;
	ТабличныйДокумент.Вывести(облИтого,1);
	
	ВыборкаЗаказчик = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаказчик.Следующий() Цикл
		облГруппировкаЗаказчик.Параметры.Заказчик = ВыборкаЗаказчик.Заказчик;
		облГруппировкаЗаказчик.Параметры.Сумма = ВыборкаЗаказчик.СуммаОстаток;
		ТабличныйДокумент.Вывести(облГруппировкаЗаказчик,2);
	
		ВыборкаДетальныеЗаписи = ВыборкаЗаказчик.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			облСтрока.Параметры.Заявка = ВыборкаДетальныеЗаписи.Заявка;
			облСтрока.Параметры.Работа = ВыборкаДетальныеЗаписи.ЗаявкаВыполненнаяРабота;
			облСтрока.Параметры.Сумма = ВыборкаДетальныеЗаписи.СуммаОстаток;
			ТабличныйДокумент.Вывести(облСтрока,3);
		КонецЦикла;
	КонецЦикла;
	ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(1);
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры
