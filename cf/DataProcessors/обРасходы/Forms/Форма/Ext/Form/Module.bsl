
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.НачалоПериода = НачалоМесяца(ТекущаяДата());
	Объект.КонецПериода  = КонецДня(КонецМесяца(ТекущаяДата()));
	СформироватьПредставлениеДаты();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()

	Обработка = РеквизитФормыВЗначение("Объект");	
	
	Макет = Обработка.ПолучитьМакет("Макет");
	
	ТабличныйДокумент.Очистить();
	
	облЗаголовок = Макет.ПолучитьОбласть("ОблЗаголовок");
	облШапка 	 = Макет.ПолучитьОбласть("облШапка");
	облСтрокаПоследняя 	 = Макет.ПолучитьОбласть("облСтрокаПоследняя");
	облИтого 	 = Макет.ПолучитьОбласть("облИтого");	

	облЗаголовок.Параметры.ПериодОтчета = "Расходы за: " + Формат(Объект.НачалоПериода,"ДФ=dd.MM.yyyy") + " - " + Формат(Объект.КонецПериода,"ДФ=dd.MM.yyyy");
	ТабличныйДокумент.Вывести(облЗаголовок);
	
	ТабличныйДокумент.Вывести(облШапка);
	
	НачалоПериода = Объект.НачалоПериода;
	КонецПериода  = Объект.КонецПериода;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходДенегОбороты.ПериодГод КАК ПериодГод,
		|	РасходДенегОбороты.ПериодМесяц КАК ПериодМесяц,
		|	РасходДенегОбороты.ПериодНеделя КАК ПериодНеделя,
		|	РасходДенегОбороты.СтатьяРасходов КАК СтатьяРасходов,
		|	СУММА(РасходДенегОбороты.СуммаРасходаОборот) КАК СуммаРасходаОборот,
		|	ВЫБОР
		|		КОГДА РасходДенегОбороты.СтатьяРасходов = &СтатьяИнвестиции
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоИнвестиции
		|ИЗ
		|	РегистрНакопления.РасходДенег.Обороты(&НачалоПериода, &КонецПериода, Авто, ) КАК РасходДенегОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходДенегОбороты.ПериодГод,
		|	РасходДенегОбороты.СтатьяРасходов,
		|	РасходДенегОбороты.ПериодНеделя,
		|	РасходДенегОбороты.ПериодМесяц,
		|	ВЫБОР
		|		КОГДА РасходДенегОбороты.СтатьяРасходов = &СтатьяИнвестиции
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодГод УБЫВ,
		|	ПериодМесяц УБЫВ,
		|	ПериодНеделя УБЫВ,
		|	ЭтоИнвестиции,
		|	СуммаРасходаОборот УБЫВ
		|ИТОГИ
		|	СУММА(СуммаРасходаОборот)
		|ПО
		|	ОБЩИЕ,
		|	ПериодГод,
		|	ПериодМесяц,
		|	ПериодНеделя";
	
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("СтатьяИнвестиции", Справочники.СтатьиРасходов.Инвестиции);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбщийИтог = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаОбщийИтог.Следующий();		// Общий итог
	
	ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
	
	облИтого.Параметры.СтатьяРасхода = "ИТОГО:";
	облИтого.Параметры.СуммаР = ВыборкаОбщийИтог.СуммаРасходаОборот;
	ТабличныйДокумент.Вывести(облИтого,1);
		
	ВыборкаПериодГод = ВыборкаОбщийИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПериодГод.Следующий() Цикл
		облИтого.Параметры.СтатьяРасхода = Формат(ВыборкаПериодГод.ПериодГод,"ДФ=yyyy") + "г.";
		облИтого.Параметры.СуммаР = ВыборкаПериодГод.СуммаРасходаОборот;
		ТабличныйДокумент.Вывести(облИтого,2);
	
		ВыборкаПериодМесяц = ВыборкаПериодГод.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаПериодМесяц.Следующий() Цикл
			облИтого.Параметры.СтатьяРасхода = Формат(ВыборкаПериодМесяц.ПериодМесяц,"ДФ='MMMM yyyy'");
			облИтого.Параметры.СуммаР = ВыборкаПериодМесяц.СуммаРасходаОборот;
			ТабличныйДокумент.Вывести(облИтого,3);
	
			ВыборкаПериодНеделя = ВыборкаПериодМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			НомерНедели = ВыборкаПериодНеделя.Количество();
			
			Пока ВыборкаПериодНеделя.Следующий() Цикл
				облИтого.Параметры.СтатьяРасхода = "Неделя: " + Строка(НомерНедели) + " (" +Формат(ВыборкаПериодНеделя.ПериодНеделя, "ДФ=dd.MM") + ")";
				облИтого.Параметры.СуммаР = ВыборкаПериодНеделя.СуммаРасходаОборот;
				ТабличныйДокумент.Вывести(облИтого,4);
	                                                              
				ВыборкаДетальныеЗаписи = ВыборкаПериодНеделя.Выбрать();
	
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Если ВыборкаДетальныеЗаписи.ЭтоИнвестиции тогда
						облСтрока = Макет.ПолучитьОбласть("ОблСтрокаИ");
						облСтрока.Параметры.СтатьяРасхода = ВыборкаДетальныеЗаписи.СтатьяРасходов;
						облСтрока.Параметры.СуммаР = ВыборкаДетальныеЗаписи.СуммаРасходаОборот;
						ТабличныйДокумент.Вывести(облСтрока,5);
					Иначе
						облСтрока = Макет.ПолучитьОбласть("ОблСтрока");
						облСтрока.Параметры.СтатьяРасхода = ВыборкаДетальныеЗаписи.СтатьяРасходов;
						облСтрока.Параметры.СуммаР = ВыборкаДетальныеЗаписи.СуммаРасходаОборот;
						ТабличныйДокумент.Вывести(облСтрока,5);	
					КонецЕсли;
				КонецЦикла;
				НомерНедели = НомерНедели - 1;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(3);
	
	ТабличныйДокумент.Вывести(облСтрокаПоследняя);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	СформироватьОтчетНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьМесяцКДате(Команда)
	
	Объект.НачалоПериода = ДобавитьМесяц(Объект.НачалоПериода,1);
	Объект.КонецПериода  = ДобавитьМесяц(Объект.КонецПериода, 1);
	СформироватьПредставлениеДаты();
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УбавитьМесяцИзДаты(Команда)
	
	Объект.НачалоПериода = ДобавитьМесяц(Объект.НачалоПериода,-1);
	Объект.КонецПериода  = ДобавитьМесяц(Объект.КонецПериода, -1);
	СформироватьПредставлениеДаты();
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеДаты()
	
	ПредставлениеДаты = Формат(Объект.НачалоПериода,"ДФ='MMMM yyyy'" );
	
КонецПроцедуры




